project.version = "$version"
project.group = "$group"

apply from: "$publish"

kotlin {
    jvm {
        compilations.main {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
    js (IR) {
        browser {
            testTask {
                useMocha {
                    timeout = "240000"
                }
            }
        }
        nodejs {
            testTask {
                useMocha {
                    timeout = "240000"
                }
            }
        }
    }
    wasmJs {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    useConfigDirectory(rootProject.relativeProjectPath("gradle/karma.config.d"))
                }
            }
        }
        nodejs {
            testTask {
                enabled = false
            }
        }
    }
    androidTarget {
        publishAllLibraryVariants()
        compilations.all {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation compose.runtime
                api libs.kt.serialization
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation libs.kt.coroutines.test
            }
        }
        jvmMain {
            dependencies {
                implementation compose.desktop.currentOs
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation compose.uiTest
            }
        }
        jsMain {
            dependencies {
                implementation compose.web.core
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        wasmJsTest {
            dependencies {
                implementation kotlin('test-wasm-js')
            }
        }
        androidUnitTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation libs.android.test.junit
                implementation libs.android.espresso
                implementation compose.uiTest
            }
        }
    }
}

apply from: "$defaultAndroidSettings"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


//compose {
//    if (composePluginKotlinVersion != null && !composePluginKotlinVersion.isEmpty()) {
//        kotlinCompilerPlugin.set(composePluginKotlinVersion)
//    }
//}
