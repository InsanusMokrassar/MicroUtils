rootProject.name='micro_utils'

String[] includes = [
    ":common",
    ":common:compose",
    ":matrix",
    ":crypto",
    ":selector:common",
    ":pagination:common",
    ":pagination:exposed",
    ":pagination:ktor:common",
    ":pagination:ktor:server",
    ":mime_types",
    ":language_codes",
    ":language_codes:generator",
    ":repos:common",
    ":repos:cache",
    ":repos:exposed",
    ":repos:inmemory",
    ":repos:ktor:client",
    ":repos:ktor:common",
    ":repos:ktor:server",
    ":ktor:server",
    ":ktor:common",
    ":ktor:client",
    ":coroutines",
    ":coroutines:compose",
    ":android:recyclerview",
    ":android:alerts:common",
    ":android:alerts:recyclerview",
    ":serialization:base64",
    ":serialization:encapsulator",
    ":serialization:typed_serializer",

    ":fsm:common",
    ":fsm:repos:common",

    ":dokka"
]


includes.each { originalName ->
    String projectDirectory = "${rootProject.projectDir.getAbsolutePath()}${originalName.replace(":", File.separator)}"
    String projectName = "${rootProject.name}${originalName.replace(":", ".")}"
    String projectIdentifier = ":${projectName}"
    include projectIdentifier
    ProjectDescriptor project = project(projectIdentifier)
    project.name = projectName
    project.projectDir = new File(projectDirectory)
}

enableFeaturePreview("VERSION_CATALOGS")
